%!GLoBES
$version="3.2.17"

nuflux(#MBminus)<
	@flux_file = "./flux/MINIBOONEminus.dat"
	@time = 1.0
	@norm = 8.87514e16
>

nuflux(#MBplus)<
	@flux_file = "./flux/MINIBOONEplus.dat"
	@time = 1.0
	@norm = 8.87514e16
>

$profiletype = 3
$densitytab = {1e-5}
$lengthtab = {0.541}


$target_mass = 1.0


$emin =         0.2
$emax =         3.0
$binsize = {0.1,0.075,0.1,0.075,0.125,0.125,0.150,0.150,0.150,0.250,1.5}



$sampling_points = 200
$sampling_min = 0.025
$sampling_max = 9.975


$filter_state = 1
$filter_value = 0.0

/*energy(#ERES_e)<>
include "./mig_gabi.dat"*/

energy(#ERES_mu)<
	@type = 1
	@sigma_e = {0.11,0.0,0.0}
>

energy(#ERES_e)<
	@type = 1
        @sigma_function = #standard
	@sigma_e = {0.0,0.3,0.0}
>

cross(#CC) < @cross_file = "./xsec/XCC.dat" >
cross(#NC) < @cross_file = "./xsec/XNC.dat" >
cross(#QE) < @cross_file = "./xsec/XQE.dat" >

/* miniboone data taken from https://www-boone.fnal.gov/for_physicists/data_release/nue2018/ */

/* for bin-dependent MiniBooNE efficiencies, see https://www-boone.fnal.gov/for_physicists/data_release/nue_nuebar_2012/efficiency/MB_nu_nubar_combined_release.html */


%energies = {0.2,0.3,0.375,0.475,0.550,0.675,0.800,0.950,1.10,1.25,1.50,3.00}

%eff = {0.135,0.139,0.131,0.123,0.116,0.106,0.102,0.095,0.089,0.082,0.026}


/* note that the binning is different for muon events and
before doing any muon neutrino analysis, the right energy
bins should be chosen */


channel(#nuea) < @channel =   #MBminus:	  +:	 m:	 e:	#QE:	 #ERES_e
                 @post_smearing_efficiencies:=%eff
>

channel(#anuea) < @channel =   #MBminus:  -:	 m:	 e:	#QE:	 #ERES_e
                @post_smearing_efficiencies:=%eff
>

channel(#nue) < @channel =   #MBplus:	  +:	 m:	 e:	#QE:	 #ERES_e
                @post_smearing_efficiencies:=%eff 
>

channel(#anue) < @channel =   #MBplus:	  -:	 m:	 e:	#QE:	 #ERES_e
                @post_smearing_efficiencies:=%eff 
>

rule(#anue_app)< @signal = 0.9@#nuea : 0.9@#anuea
                @background = 0.0@#nuea : 0.0@#anuea
                @sys_on_function = "chiMB_anue"
	        @sys_on_errors   = {}
                @sys_off_function = "chiNoSysSpectrum"
                @energy_window = 0.2 : 3.0
>

rule(#nue_app)< @signal = 0.7@#nue : 0.7@#anue
                @background = 0.0@#nue : 0.0@#anue
                @sys_on_function = "chiZero"
	        @sys_on_errors   = {}
                @sys_off_function = "chiNoSysSpectrum"
                @energy_window = 0.2 : 3.0 
>


/*
Systematic uncertainties: Signal: read from Fig. 41 of 0806.1449.
Background: Read from Fig. 1 of 1805.12028. The first two numbers
in @sys_on_errors are the energy calibration errors while the rest
are the bin-dependent systematic errors (11 signal systematics,
followed by 11 background systematics)
*/
